import org.jetbrains.kotlin.konan.target.HostManager

plugins {
  alias(libs.plugins.kotlin.multiplatform)
  alias(libs.plugins.publish)
  alias(libs.plugins.dokka)
}

def ideaActive = System.getProperty("idea.active") == "true"

kotlin {
  iosX64()
  iosArm32()
  iosArm64()
  tvosX64()
  tvosArm64()
  watchosX86()
  watchosX64()
  watchosArm32()
  watchosArm64()
  macosX64()
  mingwX86()
  mingwX64()
  linuxX64()
  linuxArm64()
  macosArm64()
  iosSimulatorArm64()
  watchosSimulatorArm64()
  tvosSimulatorArm64()

  sourceSets {
    commonMain {
      dependencies {
        api project (':runtime')
      }
    }
    commonTest {
      dependencies {
        implementation libs.kotlin.test.common
        implementation libs.testhelp
      }
    }
    nativeMain {
      dependsOn(commonMain)
      dependencies {
        api libs.sqliter
      }
    }
    nativeTest {
      dependsOn(commonTest)
      dependencies {
        implementation project(':drivers:driver-test')
      }
    }
    nativeLinuxLikeMain {
      dependsOn(nativeMain)
    }
    nativeDarwinMain{
      dependsOn(nativeLinuxLikeMain)
    }
    mingwMain{
      dependsOn(nativeMain)
    }
    mingwX86Main{
      dependsOn(mingwMain)
    }
    mingwX64Main{
      dependsOn(mingwMain)
    }
    linuxMain{
      dependsOn(nativeLinuxLikeMain)
    }
  }

  configure([targets.iosX64, targets.iosArm32, targets.iosArm64, targets.tvosX64, targets.tvosArm64, targets.watchosX86, targets.watchosX64, targets.watchosArm32, targets.watchosArm64, targets.macosX64, targets.macosArm64, targets.iosSimulatorArm64, targets.watchosSimulatorArm64, targets.tvosSimulatorArm64]) {
    sourceSets.getByName("${name}Main").dependsOn(sourceSets.nativeDarwinMain)
    sourceSets.getByName("${name}Test").dependsOn(sourceSets.nativeTest)
    compilations.test {
      kotlinOptions.freeCompilerArgs += ["-linker-options", "-lsqlite3"]
    }
  }

  configure([targets.linuxX64,targets.linuxArm32Hfp,targets.linuxArm64,targets.androidNativeArm32,targets.androidNativeArm64,targets.androidNativeX86,targets.androidNativeX64]) {
    sourceSets.getByName("${name}Main").dependsOn(sourceSets.linuxMain)
    sourceSets.getByName("${name}Test").dependsOn(sourceSets.nativeTest)
    compilations.test {
      kotlinOptions.freeCompilerArgs += ["-linker-options", "-lsqlite3 -L/usr/lib/x86_64-linux-gnu -L/usr/lib -L/usr/lib64"]
    }
  }

  configure([targets.mingwX64]) {
    sourceSets.getByName("${name}Main").dependsOn(sourceSets.mingwMain)
    sourceSets.getByName("${name}Test").dependsOn(sourceSets.nativeTest)
    compilations.test {
      kotlinOptions.freeCompilerArgs += ["-linker-options", "-Lc:\\msys64\\mingw64\\lib -L$rootDir\\libs -lsqlite3".toString()]
    }
  }

  configure([targets.mingwX86]) {
    sourceSets.getByName("${name}Main").dependsOn(sourceSets.mingwMain)
    sourceSets.getByName("${name}Test").dependsOn(sourceSets.nativeTest)
    compilations.test {
      kotlinOptions.freeCompilerArgs += ["-linker-options", "-Lc:\\msys32\\mingw32\\lib -L$rootDir\\libs -lsqlite3".toString()]
    }
  }

  //linking fails for the linux test build if not built on a linux host
  //ensure the tests and linking for them is only done on linux hosts
  tasks.named("linuxX64Test") { enabled = HostManager.hostIsLinux }
  tasks.named("linkDebugTestLinuxX64") { enabled = HostManager.hostIsLinux }

  tasks.named("mingwX64Test") { enabled = HostManager.hostIsMingw }
  tasks.named("linkDebugTestMingwX64") { enabled = HostManager.hostIsMingw }
}

apply from: "$rootDir/gradle/gradle-mvn-push.gradle"
