plugins {
  alias(deps.plugins.kotlin.js)
  alias(deps.plugins.publish)
  alias(deps.plugins.dokka)
}

kotlin {

  target {
    nodejs {
      testTask {
        useMocha {
          timeout = "5s"
        }
        filter.excludeTestsMatching("*JsWorker*")
      }
    }
    browser {
      testTask {
        useKarma {
          useChromeHeadless()
        }
      }
    }
  }

  sourceSets["main"].dependencies {
    api project(':runtime')
    //shouldn't need this but it's to avoid this bug:
    //https://youtrack.jetbrains.com/issue/KT-50503
    implementation 'org.danbrough.kotlinx:atomicfu:0.17.3'
    implementation deps.kotlin.coroutines.core
  }
  sourceSets["test"].dependencies {
    implementation deps.kotlin.test.js
    implementation npm('sql.js', deps.versions.sqljs.get())
    implementation devNpm("copy-webpack-plugin", "9.1.0")
    implementation deps.kotlin.coroutines.test
    implementation project(':extensions:async-extensions')
  }
}

apply from: "$rootDir/gradle/gradle-mvn-push.gradle"
